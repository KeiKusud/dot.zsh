# git svn で情報表示
autoload -Uz vcs_info

zstyle ':vcs_info:*' formats '[%b]'
zstyle ':vcs_info:*' actionformats '[%b|%a]'

precmd () {
  psvar=()
  LANG=en_US.UTF-8 vcs_info
  [[ -n "$vcs_info_msg_0_" ]] && psvar[1]="$vcs_info_msg_0_"
}

RPROMPT="%1(v|%F{green}%1v%f|)[%~]"

# tramp-mode
# cf: http://www.emacswiki.org/emacs/TrampMode
if [[ "$TERM" == "dumb" ]]; then
  unsetopt zle
  unsetopt prompt_cr
  unsetopt prompt_subst
  unfunction precmd
  unfunction preexec
  PS1='$ '
fi

# TeX -> PDF 一発変換
function textopdf () {
  file=$1
  if platex $file; then
    echo "\n--platex exit--\n"
    if dvipdfmx ${file%tex}dvi; then
      echo "\n--dvipdfmx exit--\n"
    else
      echo "Look ${file%tex}log"
    fi
  fi
}

# カレントディレクトリをお手軽サーバに
function server () {
  port=$1
  python -m SimpleHTTPServer $1
}

# zsh の man を表示
function zman () {
  PAGER="less -g -s '+/^       "$1"'" man zshall
}

# Emacs の現在のバッファに対応するディレクトリに cd する
# cf: http://d.hatena.ne.jp/syohex/20111026/1319606395
function cde () {
  EMACS_CWD=`emacsclient -e "
      (if (featurep 'elscreen)
          (elscreen-current-directory)
        (non-elscreen-current-directory))" | sed 's/^"\(.*\)"$/\1/'`

  echo "chdir to $EMACS_CWD"
  cd "$EMACS_CWD"
}

# tmuxの環境変数をリフレッシュする
# もっといい策を考えたい！
function tmux-session-refresh () {
  if [ -n "$TMUX" ]; then
    unset $(tmux show-env | sed -n 's/^-//p')
    eval export $(tmux show-env | sed -n 's/$/"/; s/=/="/p')
  fi
}

# bundle rake の補完設定
# cf: http://hico-horiuchi.hateblo.jp/entry/20131213/1386862270
# TODO: ここにあるのおかしい気がするので正しい場所へ
_rake_refresh () {
  if [[ ! -f .rake_tasks ]]; then
    return 0;
  else
    return $([[ Rakefile -nt .rake_tasks ]])
  fi
}

_rake () {
  if [[ -f Rakefile ]]; then
    if _rake_refresh; then
      bundle exec rake --silent --tasks | cut -d " " -f 2 > .rake_tasks
      sed -i -e 's/:/\\\:/g' -e 's/\[/\\[/g' -e 's/\]/\\]/g' .rake_tasks
    fi
    _values "rake" $(<.rake_tasks)
  fi
}

be () { bundle exec $* }

_be () {
  if [[ -d vendor/bin ]]; then
    _arguments '*: :->'
    if [[ $CURRENT == 2 ]]; then
      _values "bundle exec" `\ls -1 vendor/bin`
    elif [[ $CURRENT == 3 && $line[1] == "rake" ]]; then
      _rake
    else
      _files
    fi
  fi
}
compdef _be be

bunin () {
  bundle install --path=vendor/bundle --binstubs=vendor/bin
}
